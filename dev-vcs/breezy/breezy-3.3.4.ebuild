# Copyright 2022-2023 Orson Teodoro <orsonteodoro@hotmail.com>
# Copyright 1999-2023 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

# The lockfile should be updated once a week for security reasons.

EAPI=8

# Generated by "convert-cargo-lock.sh 3.3.4 3.3.4"
CRATES="
aho-corasick-1.1.2
autocfg-1.1.0
bitflags-1.3.2
cfg-if-1.0.0
heck-0.4.1
indoc-2.0.4
lazy_static-1.4.0
libc-0.2.150
lock_api-0.4.11
memchr-2.6.4
memoffset-0.9.0
once_cell-1.18.0
parking_lot-0.12.1
parking_lot_core-0.9.9
proc-macro2-1.0.70
pyo3-0.20.0
pyo3-build-config-0.20.0
pyo3-ffi-0.20.0
pyo3-macros-0.20.0
pyo3-macros-backend-0.20.0
quote-1.0.33
redox_syscall-0.4.1
regex-1.10.2
regex-automata-0.4.3
regex-syntax-0.8.2
scopeguard-1.2.0
smallvec-1.11.2
syn-2.0.39
target-lexicon-0.12.12
unicode-ident-1.0.12
unindent-0.2.3
windows_aarch64_gnullvm-0.48.5
windows_aarch64_msvc-0.48.5
windows_i686_gnu-0.48.5
windows_i686_msvc-0.48.5
windows-targets-0.48.5
windows_x86_64_gnu-0.48.5
windows_x86_64_gnullvm-0.48.5
windows_x86_64_msvc-0.48.5
"

DISTUTILS_USE_PEP517="setuptools"
PYTHON_COMPAT=( python3_{10..11} )
inherit autotools cargo distutils-r1 lcnr

IUSE+=" doc developer-docs user-docs"
REQUIRED_USE+="
	doc? (
		^^ (
			developer-docs
			user-docs
		)
	)
	developer-docs? (
		doc
	)
	user-docs? (
		doc
	)
"

_breezy_ebuild_set_globals() {
einfo "Called _breezy_ebuild_set_globals"
	if [[ "${USE}" =~ "developer-docs" ]] ; then
einfo "Building with developer-docs"
		distutils_enable_sphinx "doc/developers"
	elif [[ "${USE}" =~ "user-docs" ]] ; then
einfo "Building with user-docs"
		distutils_enable_sphinx "doc/en"
	fi
}
_breezy_ebuild_set_globals
unset -f _breezy_ebuild_set_globals

DESCRIPTION="Breezy is a friendly powerful distributed version control system."
HOMEPAGE="https://launchpad.net/brz"
LICENSE="
	GPL-2+
	Apache-2.0
"
# tools/rst2pdf.py is Apache-2.0
LICENSE+="
	custom
	Apache-2.0
	Apache-2.0-with-LLVM-exceptions
	BSD
	BSD-4
	GPL-2
	MIT
	PSF-2.4
	openssl
	tcltk
	Unicode-DFS-2016
	Unlicense
" # Third party cargo licenses
# homedir/.cargo/registry/src/github.com-1ecc6299db9ec823/pyo3-0.15.2 - custom
# homedir/.cargo/registry/src/github.com-1ecc6299db9ec823/regex-1.6.0 - custom
# homedir/.cargo/registry/src/github.com-1ecc6299db9ec823/pkg-version-1.0.0 - custom
# homedir/.cargo/registry/src/github.com-1ecc6299db9ec823/regex-syntax-0.6.27/src/unicode_tables/LICENSE-UNICODE - Unicode-DFS-2016
# homedir/.cargo/registry/src/github.com-1ecc6299db9ec823/unicode-ident-1.0.5/LICENSE-UNICODE - Unicode-DFS-2016
KEYWORDS="~amd64 ~x86"
SLOT="0"
IUSE+=" cext fastimport git github gpg launchpad sftp test workspace r1"
REQUIRED_USE+="
	${PYTHON_REQUIRED_USE}
	test? (
		sftp
	)
"
# U 22.04
# See also:  https://github.com/breezy-team/breezy/blob/upstream-3.3.4/setup.py#L60
# TODO package:
# launchpadlib
# setuptools-gettext
DULWICH_PV="0.21.6"
FLAKE8_PV="4.0.1"
PARAMIKO_PV="2.9.3"
CARGO_BINDINGS_DEPEND="
	elibc_glibc? (
		>=sys-libs/glibc-2.35
	)
	elibc_musl? (
		>=sys-libs/musl-1.1.24
	)
"
DEPEND="
	${PYTHON_DEPS}
	${CARGO_BINDINGS_DEPEND}
	>=dev-python/configobj-5.0.6[${PYTHON_USEDEP}]
	>=dev-python/cryptography-3.4.8[${PYTHON_USEDEP}]
	>=dev-python/dulwich-${DULWICH_PV}[${PYTHON_USEDEP}]
	>=dev-python/fastbencode-0.0.5[${PYTHON_USEDEP}]
	>=dev-python/urllib3-1.24.1[${PYTHON_USEDEP}]
	>=dev-python/merge3-0.0.8[${PYTHON_USEDEP}]
	>=dev-python/patiencediff-0.2.13[${PYTHON_USEDEP}]
	>=dev-python/pyyaml-5.4.1[${PYTHON_USEDEP}]
	>=dev-python/urllib3-1.24.1
	fastimport? (
		>=dev-python/fastimport-0.9.14[${PYTHON_USEDEP}]
	)
	github? (
		>=dev-python/PyGithub-1.55[${PYTHON_USEDEP}]
	)
	gpg? (
		>=app-crypt/gpgme-1.13.1[${PYTHON_USEDEP}]
	)
	launchpad? (
		>=dev-python/launchpadlib-1.6.3[${PYTHON_USEDEP}]
	)
	sftp? (
		>=dev-python/paramiko-${PARAMIKO_PV}[${PYTHON_USEDEP}]
	)
	workspace? (
		>=dev-python/pyinotify-0.9.6[${PYTHON_USEDEP}]
	)
"
RDEPEND+="
	${DEPEND}
"
BDEPEND+="
	${PYTHON_DEPS}
	>=dev-python/setuptools-60[${PYTHON_USEDEP}]
	>=dev-python/setuptools-rust-1.2.0[${PYTHON_USEDEP}]
	>=dev-python/setuptools-gettext-0.1.4[${PYTHON_USEDEP}]
	>=dev-python/flake8-${FLAKE8_PV}[${PYTHON_USEDEP}]
	>=dev-python/mypy-0.942[${PYTHON_USEDEP}]
	>=dev-python/packaging-21.3[${PYTHON_USEDEP}]
	cext? (
		>=dev-python/cython-0.29[${PYTHON_USEDEP}]
	)
	doc? (
		>=dev-python/docutils-0.17.1[${PYTHON_USEDEP}]
		>=dev-python/sphinx-4.3.2[${PYTHON_USEDEP}]
		>=dev-python/sphinx-epytext-0.0.4[${PYTHON_USEDEP}]
	)
	test? (
		>=dev-python/dulwich-${DULWICH_PV}[${PYTHON_USEDEP}]
		>=dev-python/flake8-${FLAKE8_PV}[${PYTHON_USEDEP}]
		>=dev-python/testscenarios-0.5.0[${PYTHON_USEDEP}]
		>=dev-python/testtools-2.5.0[${PYTHON_USEDEP}]
		>=dev-python/subunit-1.4.0[${PYTHON_USEDEP}]
		sftp? (
			>=dev-python/paramiko-${PARAMIKO_PV}[${PYTHON_USEDEP},server]
		)
	)
"
SRC_URI="
$(cargo_crate_uris ${CRATES})
https://launchpad.net/brz/$(ver_cut 1-2 ${PV})/${PV}/+download/${PN}-${PV}.tar.gz
"
S="${WORKDIR}/${PN}-${PV}"
RESTRICT="mirror"
PATCHES=(
	"${FILESDIR}/breezy-3.3.3-fix-compile-test.patch" # Added by oiledmachine-overlay
)

check_network_sandbox() {
	if has network-sandbox $FEATURES ; then
eerror
eerror "FEATURES=\"-network-sandbox\" must be added per-package env to be able"
eerror "to update lockfile."
eerror
		die
	fi
}

check_usersandbox() {
#
# The error message:
#
# * /var/tmp/portage/sys-apps/sandbox-2.29/work/sandbox-2.29/libsandbox/libsandbox.c:resolve_path():240: failure (Cannot allocate memory):
# * malloc(8192)
#
	if has usersandbox $FEATURES ; then
eerror
eerror "FEATURES=\"-usersandbox\" must be added per-package env to be able"
eerror "to run tests."
eerror
		die
	fi
}

pkg_setup() {
	[[ "${GENERATE_LOCKFILE}" == "1" ]] && check_network_sandbox
	python_setup
	if use test ; then
		check_usersandbox
		use sftp && check_network_sandbox
	fi
}

_lockfile_gen_unpack() {
	unpack "${PN}-${PV}.tar.gz"
	cd "${S}" || die
einfo "Generating lockfile"
	rm Cargo.lock
	cargo generate-lockfile || die "Failed to update Cargo.lock"
	die
}

_production_unpack() {
	unpack "${PN}-${PV}.tar.gz"
	if [[ -e "${FILESDIR}/${PV}/Cargo.lock" ]] ; then
einfo "Adding Cargo.lock"
		cp -a "${FILESDIR}/${PV}/Cargo.lock" "${S}" || die
	fi
	cargo_src_unpack
}

src_unpack() {
	default
	if [[ "${GENERATE_LOCKFILE}" == "1" ]] ; then
		_lockfile_gen_unpack
	else
		_production_unpack
	fi
}

src_test() {
	local d="${WORKDIR}/${PN}-${PV}-${EPYTHON/./_}"
	cd "${S}" || die
	PYTHONHASHSEED=random \
	BRZ_PLUGIN_PATH="-site:-user" \
	PYTHONPATH="${d}/install/usr/lib/${EPYTHON}/site-packages:${PYTHONPATH}" \
	"${d}/install/usr/bin/brz" selftest || die
}

src_install() {
	LCNR_SOURCE="${WORKDIR}/cargo_home/gentoo"
	LCNR_TAG="third_party"
	lcnr_install_files

	distutils-r1_src_install
}

# OILEDMACHINE-OVERLAY-META:  CREATED-EBUILD
# OILEDMACHINE-OVERLAY-TEST:  PASSED (test suite) 3.3.3 (20230617)
# USE="sftp test -cext (-debug) -developer-docs -doc -fastimport -git -github
# -gpg -launchpad -r1 -user-docs -workspace" PYTHON_TARGETS="python3_10
# -python3_11"
#
# Ran 33728 tests in 2411.236s
#
# OK (known_failures=46)
# 2136 tests skipped
#
