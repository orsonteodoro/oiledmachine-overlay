# Copyright 2022-2023 Orson Teodoro <orsonteodoro@hotmail.com>
# Copyright 1999-2023 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

# The lockfile should be updated once a week for security reasons.

EAPI=8

# Generated by "convert-cargo-lock.sh 3.3.3"
CRATES="
aho-corasick-1.0.2
autocfg-1.1.0
bitflags-1.3.2
cfg-if-1.0.0
indoc-1.0.9
lazy_static-1.4.0
libc-0.2.146
lock_api-0.4.10
memchr-2.5.0
memoffset-0.9.0
once_cell-1.18.0
parking_lot-0.12.1
parking_lot_core-0.9.8
proc-macro2-1.0.60
pyo3-0.19.0
pyo3-build-config-0.19.0
pyo3-ffi-0.19.0
pyo3-macros-0.19.0
pyo3-macros-backend-0.19.0
quote-1.0.28
redox_syscall-0.3.5
regex-1.8.4
regex-syntax-0.7.2
scopeguard-1.1.0
smallvec-1.10.0
syn-1.0.109
target-lexicon-0.12.7
unicode-ident-1.0.9
unindent-0.1.11
windows-targets-0.48.0
windows_aarch64_gnullvm-0.48.0
windows_aarch64_msvc-0.48.0
windows_i686_gnu-0.48.0
windows_i686_msvc-0.48.0
windows_x86_64_gnu-0.48.0
windows_x86_64_gnullvm-0.48.0
windows_x86_64_msvc-0.48.0
"

DISTUTILS_USE_PEP517="setuptools"
PYTHON_COMPAT=( python3_{8..11} )
inherit autotools cargo distutils-r1 lcnr

DESCRIPTION="Breezy is a friendly powerful distributed version control system."
HOMEPAGE="https://launchpad.net/brz"
LICENSE="
	GPL-2+
	Apache-2.0
"
# tools/rst2pdf.py is Apache-2.0
LICENSE+="
	custom
	Apache-2.0
	Apache-2.0-with-LLVM-exceptions
	BSD
	BSD-4
	GPL-2
	MIT
	PSF-2.4
	openssl
	tcltk
	Unicode-DFS-2016
	Unlicense
" # Third party cargo licenses
# homedir/.cargo/registry/src/github.com-1ecc6299db9ec823/pyo3-0.15.2 - custom
# homedir/.cargo/registry/src/github.com-1ecc6299db9ec823/regex-1.6.0 - custom
# homedir/.cargo/registry/src/github.com-1ecc6299db9ec823/pkg-version-1.0.0 - custom
# homedir/.cargo/registry/src/github.com-1ecc6299db9ec823/regex-syntax-0.6.27/src/unicode_tables/LICENSE-UNICODE - Unicode-DFS-2016
# homedir/.cargo/registry/src/github.com-1ecc6299db9ec823/unicode-ident-1.0.5/LICENSE-UNICODE - Unicode-DFS-2016
KEYWORDS="~amd64 ~x86"
SLOT="0"
IUSE+=" cext doc fastimport git github gpg launchpad sftp test workspace r1"
REQUIRED_USE+="
	${PYTHON_REQUIRED_USE}
"
# See also:  https://github.com/breezy-team/breezy/blob/upstream-3.3.3/setup.py#L60
DEPEND="
	${PYTHON_DEPS}
	>=dev-python/configobj-5.0.8[${PYTHON_USEDEP}]
	>=dev-python/cryptography-40.0.2[${PYTHON_USEDEP}]
	>=dev-python/dulwich-0.21.3[${PYTHON_USEDEP}]
	>=dev-python/fastbencode-0.2[${PYTHON_USEDEP}]
	>=dev-python/urllib3-2.0.2[${PYTHON_USEDEP}]
	>=dev-python/merge3-0.0.13[${PYTHON_USEDEP}]
	>=dev-python/patiencediff-0.2.13[${PYTHON_USEDEP}]
	>=dev-python/pyyaml-6.0[${PYTHON_USEDEP}]
	fastimport? (
		>=dev-python/fastimport-0.9.14[${PYTHON_USEDEP}]
	)
	github? (
		dev-python/PyGithub[${PYTHON_USEDEP}]
	)
	gpg? (
		>=app-crypt/gpgme-1.16.0[${PYTHON_USEDEP}]
	)
	launchpad? (
		>=dev-python/launchpadlib-1.11.0[${PYTHON_USEDEP}]
	)
	sftp? (
		>=dev-python/paramiko-3.1.0[${PYTHON_USEDEP}]
		dev-python/pycrypto[${PYTHON_USEDEP}]
	)
	workspace? (
		>=dev-python/pyinotify-0.9.6[${PYTHON_USEDEP}]
	)
"
RDEPEND+="
	${DEPEND}
"
# TODO package:
# launchpadlib
# setuptools-gettext
FLAKE8_PV="6.0.0"
BDEPEND+="
	${PYTHON_DEPS}
	>=dev-python/setuptools-60[${PYTHON_USEDEP}]
	>=dev-python/setuptools-rust-1.6.0[${PYTHON_USEDEP}]
	>=dev-python/setuptools-gettext-0.1.3[${PYTHON_USEDEP}]
	>=dev-python/flake8-${FLAKE8_PV}[${PYTHON_USEDEP}]
	>=dev-python/mypy-1.3.0[${PYTHON_USEDEP}]
	>=dev-python/packaging-23.1[${PYTHON_USEDEP}]
	cext? (
		>=dev-python/cython-0.29.34[${PYTHON_USEDEP}]
	)
	doc? (
		>=dev-python/docutils-0.20.1[${PYTHON_USEDEP}]
		>=dev-python/sphinx-7.0.1[${PYTHON_USEDEP}]
		>=dev-python/sphinx-epytext-0.0.4[${PYTHON_USEDEP}]
	)
	test? (
		>=dev-python/dulwich-0.21.3[${PYTHON_USEDEP}]
		>=dev-python/flake8-${FLAKE8_PV}[${PYTHON_USEDEP}]
		>=dev-python/testscenarios-0.5.0[${PYTHON_USEDEP}]
		>=dev-python/testtools-2.6.0[${PYTHON_USEDEP}]
		>=dev-python/subunit-1.4.2[${PYTHON_USEDEP}]
	)
"
SRC_URI="
$(cargo_crate_uris ${CRATES})
https://launchpad.net/brz/$(ver_cut 1-2 ${PV})/${PV}/+download/${PN}-${PV}.tar.gz
"
S="${WORKDIR}/${PN}-${PV}"
RESTRICT="mirror"

check_network_sandbox() {
	if has network-sandbox $FEATURES ; then
eerror
eerror "FEATURES=\"-network-sandbox\" must be added per-package env to be able"
eerror "to update lockfile."
eerror
		die
	fi
}

check_usersandbox() {
#
# The error message:
#
# * /var/tmp/portage/sys-apps/sandbox-2.29/work/sandbox-2.29/libsandbox/libsandbox.c:resolve_path():240: failure (Cannot allocate memory):
# * malloc(8192)
#
	if has usersandbox $FEATURES ; then
eerror
eerror "FEATURES=\"-usersandbox\" must be added per-package env to be able"
eerror "to run tests."
eerror
		die
	fi
}

pkg_setup() {
	[[ "${GENERATE_LOCKFILE}" == "1" ]] && check_network_sandbox
	python_setup
	use test && check_usersandbox
}

_lockfile_gen_unpack() {
	unpack "${PN}-${PV}.tar.gz"
	cd "${S}" || die
einfo "Generating lockfile"
	rm Cargo.lock
	cargo generate-lockfile || die "Failed to update Cargo.lock"
	die
}

_production_unpack() {
	unpack "${PN}-${PV}.tar.gz"
	if [[ -e "${FILESDIR}/${PV}/Cargo.lock" ]] ; then
einfo "Adding Cargo.lock"
		cp -a "${FILESDIR}/${PV}/Cargo.lock" "${S}" || die
	fi
	cargo_src_unpack
}

src_unpack() {
	default
	if [[ "${GENERATE_LOCKFILE}" == "1" ]] ; then
		_lockfile_gen_unpack
	else
		_production_unpack
	fi
}

src_test() {
	local d="${WORKDIR}/${PN}-${PV}-${EPYTHON/./_}"
	cd "${d}/install/usr/bin" || die
	PYTHONPATH="${d}/install/usr/lib/${EPYTHON}/site-packages:${PYTHONPATH}" \
	./brz selftest || die
}

src_install() {
	LCNR_SOURCE="${WORKDIR}/cargo_home/gentoo"
	LCNR_TAG="third_party"
	lcnr_install_files

	distutils-r1_src_install
}

# OILEDMACHINE-OVERLAY-META:  CREATED-EBUILD
# OILEDMACHINE-OVERLAY-TEST:  FAILED (test suite) 3.3.3 (20230611)
# Ran 33248 tests in 2344.151s

# FAILED (failures=5, errors=26, known_failure_count=45)
# 2121 tests skipped
